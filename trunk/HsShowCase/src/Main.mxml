<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" 
				xmlns:closableTab="org.hamster.closableTab.*"
				layout="absolute" minWidth="955" minHeight="600" frameRate="60"
				backgroundColor="0xaeaeae"
				preinitialize="app_preinitializeHandler(event)"
				applicationComplete="app_applicationCompleteHandler(event)"
				creationComplete="application1_creationCompleteHandler(event)"
				>
	<mx:Metadata>
		[Event(name="caseList_Change")]
		[ResourceBundle("app")]
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			import mx.events.IndexChangedEvent;
			import mx.events.ListEvent;
			import mx.logging.ILogger;
			import mx.logging.Log;
			
			import org.hamster.closableTab.HsClosableTabEvent;
			import org.hamster.showcase.common.facade.AppFacade;
			import org.hamster.showcase.common.util.CommonUtil;
			import org.hamster.showcase.common.util.SWFProfiler;
			import org.hamster.showcase.common.util.TracePanelUtil;
			import org.hamster.showcase.main.view.ModuleLoaderContainer;
			import org.hamster.showcase.main.vo.CaseVO;
			
			private var facade:AppFacade = AppFacade.instance;
			private static var logger:ILogger = Log.getLogger("hs.Main");
			
			[Bindable] public var caseVOList:ArrayCollection;
			
			protected function app_preinitializeHandler(event:FlexEvent):void
			{
				CommonUtil.facade = this.facade;
				CommonUtil.setLocale("en_US");
			}
			
			protected function app_applicationCompleteHandler(event:FlexEvent):void
			{
				SWFProfiler.init(this.stage, this);
				TracePanelUtil.instance.init(this.stage, this);
				
				facade.sendNotification(AppFacade.INIT, this);
				
				logger.info("App init Complete");
			}


			protected function caseList_changeHandler(event:ListEvent):void
			{
				this.dispatchEvent(new Event("caseList_Change"));
			}
			
			protected function caseList_dataChangeHandler(event:FlexEvent):void
			{
				if (caseNavigator.numChildren > 1) {
					caseNavigator.closable = true;
				}
			}

			protected function caseNavigator_closeTabHandler(event:HsClosableTabEvent):void
			{
				caseNavigator.removeChildAt(event.index);
				if (caseNavigator.numChildren <= 1) {
					caseNavigator.closable = false;
					return;
				}
			}

			public function addNewModule(modulePath:String):void
			{
				var moduleLoaderContainer:ModuleLoaderContainer = new ModuleLoaderContainer();
				moduleLoaderContainer.loadModule(modulePath);
				moduleLoaderContainer.label = modulePath;
				
				caseNavigator.addChild(moduleLoaderContainer);
				caseNavigator.closable = caseNavigator.numChildren > 1;
			}

			protected function caseNavigator_changeHandler(event:IndexChangedEvent):void
			{
				var child:ModuleLoaderContainer = ModuleLoaderContainer(event.relatedObject);
				for (var i:int = 0; i < caseVOList.length; i++) {
					var caseVO:CaseVO = CaseVO(caseVOList[i]);
					if (child.url == caseVO.moduleLocation) {
						caseList.selectedIndex = i;	
					}
				}
			}


			protected function localeComboBox_changeHandler(event:ListEvent):void
			{
				CommonUtil.setLocale(String(localeComboBox.selectedItem));
			}


			protected function application1_creationCompleteHandler(event:FlexEvent):void
			{
				// TODO Auto-generated method stub
			}

		]]>
	</mx:Script>
	<mx:VBox width="100%" height="100%">
		<mx:HBox width="100%" verticalAlign="middle">
			<mx:Label text="{this.resourceManager.getString('app', 'app.title')}" />
			<mx:ComboBox id="localeComboBox" dataProvider="{CommonUtil.LOCALES}" 
						 selectedIndex="0" change="localeComboBox_changeHandler(event)" />
		</mx:HBox>
		<mx:HDividedBox width="100%" height="100%">
			<mx:List id="caseList" dataProvider="{caseVOList}" labelField="name" width="150" height="100%"
					 dataChange="caseList_dataChangeHandler(event)"
					 change="caseList_changeHandler(event)"/>
			<!--<mx:Canvas id="caseContainer" width="100%" height="100%" />-->
			<closableTab:HsClosableTabNavigator id="caseNavigator" width="100%" height="100%"
												autoRemoveChild="false" 
												change="caseNavigator_changeHandler(event)"
												closeTab="caseNavigator_closeTabHandler(event)">
			</closableTab:HsClosableTabNavigator>
		</mx:HDividedBox>
	</mx:VBox>
</mx:Application>
